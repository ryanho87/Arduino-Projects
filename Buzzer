//#include <EEPROM.h>
#include <ESP8266WiFi.h>
#include <PubSubClient.h>

const char* ssid = "DAY5WHOADAYS";
const char* password = "TenZeros";
const char* mqtt_server = "192.168.42.98";
const char* topic_state_buzzer = "home-assistant/lock/buzzer/state";
const char* topic_state_motion = "home-assistant/sensor/hallway_sensor_west";
const char* state_on = "1";
const char* state_off = "0";

int in1 = 16;                 //Set signal channel for relay
int inputPin = 2;             // choose the input pin (for PIR sensor)
int pirState = LOW;           // we start, assuming no motion detected
int val = 0;

WiFiClient espClient;
PubSubClient client(espClient);

void setup() {
  Serial.begin(115200);       //Connect to Serial Port

  while (! Serial);           //Wait until we are connected to Serial Port
  Serial.println();

  //Connect to Wifi
  delay(10);
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());

  client.setServer(mqtt_server, 1883);      //Connect to MQTT Server
  client.setCallback(callback);

  pinMode(in1, OUTPUT);         // declare relay pin as output
  pinMode(inputPin, INPUT);     // declare sensor pin as input
  digitalWrite(in1, LOW);       //Set relay to start in off state

}

void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");

  if (strcmp(topic, "home-assistant/lock/buzzer") == 0) {
    for (int i = 0; i < length; i++) {
      char receivedChar = (char)payload[i];
      Serial.print(receivedChar);

      if (receivedChar == '0') {
        digitalWrite(in1, LOW);
        client.publish(topic_state_buzzer, state_off);
      }
      if (receivedChar == '1') {
        digitalWrite(in1, HIGH);
        client.publish(topic_state_buzzer, state_on);
      }
    }
    Serial.println();
  }
}

void reconnect() {
  while (!client.connected()) {                          // Loop until we're reconnected
    Serial.print("Attempting MQTT connection...");
    if (client.connect("ESP8266Client")) {               // Attempt to connect
      Serial.println("connected");
      client.subscribe("home-assistant/lock/buzzer");    // ... and subscribe to topic(s)
      return;
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 2 seconds");
      delay(2000);
    }
  }
}

void loop() {
  if (!client.connected()) {
    reconnect();
    }
    val = digitalRead(inputPin);                      // read input value
    if (val == HIGH) {                                // check if the input is HIGH
      if (pirState == LOW) {
        // we have just turned on
        Serial.println("Motion detected!");
        client.publish(topic_state_motion, state_on);
        // We only want to print on the output change, not state
        pirState = HIGH;
      }
    } else {
      if (pirState == HIGH){
        // we have just turned of
        Serial.println("Motion ended!");
        client.publish(topic_state_motion, state_off);
        // We only want to print on the output change, not state
        pirState = LOW;
      }
    }
  client.loop();
}
